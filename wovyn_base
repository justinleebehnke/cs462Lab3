ruleset wovyn_base {
  meta {
    
  }
  
  global {
    temperature_threshold = 76.6
    num_to_notify = "9528739729"
    num_from = "5072487466"
  }
  
  rule process_heartbeat {
    select when wovyn heartbeat
    pre {
      genericThing = event:attrs{"genericThing"}.klog("genericThing")
    }
    if genericThing then
      send_directive("Wovyn Heartbeat Detected")
    fired {
      raise wovyn event "new_temperature_reading"
      attributes { 
        "temperature":genericThing{"data"}{"temperature"}[0]{"temperatureF"}.klog("Current Temp: "), 
        "timestamp": time:now()
      }
    }
  }
  
  rule find_high_temps {
    select when wovyn new_temperature_reading
    pre {
      tempF = event:attrs{"temperature"}
      timestamp = event:attrs{"timestamp"}
      message = (tempF > temperature_threshold) => "Threshold Violated" | "Temperature Normal"
    }
    send_directive(message)
    always {
      raise wovyn event "threshold_violation"
      attributes { 
        "temperature":tempF, 
        "timestamp": time:now()
      } if (message == "Threshold Violated");
    }
  }
  
  rule threshold_notification {
    select when wovyn threshold_violation
    pre {
      tempF = event:attrs{"temperature"}.klog("VIOLATED TEMP")
      timestamp = event:attrs{"timestamp"}.klog("VIOLATED TIME")
    }
    always {
      raise test event "new_message"
      attributes {
        "to":num_to_notify,
        "from": num_from,
        "message": "On " + time:strftime(timestamp, "%A %d %b %Y")  + " your temp sensor reported a reading of " + 
        tempF + " degrees which is above the current threshold of " + temperature_threshold + " degrees."
      }
    }
  }
}
